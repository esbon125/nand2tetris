// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];
    
    PARTS:
     
     //inc
    Inc16(in=rout, out=inc1);
    Mux16(a=rout, b=inc1, sel=inc, out=incout);
   
    //load
    Mux16(a=incout, b=in, sel=load, out=loadout);
    
    //reset
    And16(a=loadout, b=false, out=zin);
    Mux16(a=loadout, b=zin, sel=reset, out=resetout);

    //load control bit for Register
    Or(a=load, b=inc, out=temp);
    Or(a=temp, b=reset, out=sel);
   
        
   Register(in=resetout, load=sel, out=out,out=rout);      
}
