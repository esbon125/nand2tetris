// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   //x
    //Operations
    And16(a=x, b=false, out=zerox);
    Not16(in=muxzx, out=notmuxzx);
        
    //Multiplexers
    Mux16(a=x, b=zerox,sel=zx, out=muxzx);
    Mux16(a=muxzx, b=notmuxzx, sel=nx, out=muxnx);
            
    //y
    //Operations
    And16(a=y, b=false, out=zeroy);
    Not16(in=muxzy, out=notmuxzy);
    //Multiplexers
    Mux16(a=y, b=zeroy,sel=zy, out=muxzy);
    Mux16(a=muxzy, b=notmuxzy, sel=ny, out=muxny);
    
    //f
    Add16(a=muxnx, b=muxny, out=fxaddy);
    And16(a=muxnx, b=muxny, out=fxandy);
    Mux16(a=fxandy, b=fxaddy, sel=f, out=muxf);  
    //no
    Not16(in=muxf, out=notmuxf);
    Mux16(a=muxf, b=notmuxf, sel=no, out=output,out=out);

    //ng and zr 
    Or16Way(in=output,out=outselzr);
    Mux(a=true, b=false, sel=outselzr, out=zr); 
    
   And16(a=output, b[0..14]=false,b[15]=true, out=sel16);
   Or16Way(in=sel16,out=outselng);           
    Mux(a=false,b=true,sel=outselng,out=ng);   
    }